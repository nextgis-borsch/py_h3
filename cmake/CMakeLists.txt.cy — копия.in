list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR})

include_directories(${CMAKE_CURRENT_SOURCE_DIR})

macro(add_cython_file filename)
    add_custom_command(
      OUTPUT "${filename}.c"
      COMMENT
        "Making ${CMAKE_CURRENT_BINARY_DIR}/${filename}.c from ${CMAKE_CURRENT_SOURCE_DIR}/${filename}.pyx"
      COMMAND Python::Interpreter -m cython
              "${CMAKE_CURRENT_SOURCE_DIR}/${filename}.pyx" --output-file "${filename}.c" -I ${CMAKE_CURRENT_SOURCE_DIR}
      DEPENDS "${filename}.pyx"
      VERBATIM)

    python_add_library(${filename} MODULE "${filename}.c" WITH_SOABI)

    set_property(TARGET ${filename} PROPERTY C_STANDARD 99)
    target_link_libraries(${filename} PRIVATE h3)
    install(TARGETS ${filename} LIBRARY DESTINATION ${SKBUILD_PROJECT_NAME}/_cy)
endmacro()

add_cython_file(util)
add_cython_file(geo)
add_cython_file(cells)
add_cython_file(edges)
add_cython_file(to_multipoly)
add_cython_file(unstable_vect)

# Include pyx and pxd files in distribution for use by Cython API
install(
    FILES
        cells.pyx
        edges.pyx
        geo.pyx
        unstable_vect.pyx
        util.pxd
        util.pyx
        to_multipoly.pyx
    DESTINATION
        ${SKBUILD_PROJECT_NAME}/_cy
)